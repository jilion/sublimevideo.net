SublimeVideo.homeReady = ->
  SublimeVideo.setupHomeSublime()
  SublimeVideo.pickRandomVideoThumb()
  # (new SublimeVideo.Quotes).randomShow() if $('section.showcase').exists()

SublimeVideo.setupHomeSublime = ->
  homeDemo = new SublimeVideo.HomeDemo("#home_demo_player")
  sublime.ready ->
    homeDemo.setupObservers()

  if $("#customers-quotes-slideshow")
    $("#customers-quotes-slideshow > li:gt(0)").hide().addClass("blur")

    setInterval ->
      $el = $('#customers-quotes-slideshow > li:first')
      $el.addClass("blur").fadeOut 800, ->
        $el.next().fadeIn(800).removeClass("blur")
        $el.appendTo('#customers-quotes-slideshow')
    , 9000


# SublimeVideo.postersPaths = <%= (1..6).inject([]) { |memo, n| memo << image_path("home/video/posters/poster_#{n}.jpg") } %>

SublimeVideo.pickRandomVideoThumb = ->
  if ($homeVideo = $('#home_video')).exists()
    imgIndex = Math.floor(Math.random()*6)

    thumbs = $homeVideo.find('img.video_thumb')
    thumbs.hide()

    # Lightbox thumb
    $(thumbs[imgIndex]).show()
    $homeVideo.show()

    # Lightbox poster
    unless SublimeVideo.isMobile()
      posterImage = new Image()
      posterImage.src = SublimeVideo.postersPaths[imgIndex]
    $video = $("#video_horizon")
    $video.attr("poster", SublimeVideo.postersPaths[imgIndex])

SublimeVideo.fadeInHackForChrome = ($div) ->
  if /Chrome/i.test(navigator.userAgent)
    $div.css('left', '49px')
    setTimeout((-> $div.css('left', '50px')), 10)


class SublimeVideo.HomeDemo
  constructor: (player) ->
    @player = $(player)
    @features = $('ul.demo-video-actions')
    @kitSelector = $('ul.demo-choose-design')
    # @setupObservers()

  setupObservers: ->
    @features.find('li a').each (index, element) =>
      if $(element).data('feature') == 'lightbox'
        @setupLightbox(element)
      else
        @manageTogglerBtn(element, 'feature')
    @kitSelector.find('li a').each (index, element) =>
      @manageTogglerBtn(element, 'kit')

  manageTogglerBtn: (element, attribute) ->
    btn = $(element)
    autoplay = true
    btn.on 'click', (event) =>
      if attribute is "kit"
        @kitSelector.find('li').removeClass('active')
        autoplay = false
      event.preventDefault()
      @setupAttribute btn.data(attribute), autoplay
      btn.parent().toggleClass('active')

  setupAttribute: (value, autoplay) ->
    @unprepareVideo()
    @updatePlayersValues(@player, value)
    @updatePlayersValues(@lightboxPlayer, value)
    @prepareAndPlayVideo(autoplay)

  updatePlayersValues: (player, value) ->
    switch value
      when 9, 10, 11
        player.attr('data-player-kit',value)
      when "youtube"
        if player.attr('data-youtube-id')?
          player.removeAttr('data-youtube-id')
        else
          player.attr('data-youtube-id','rAq2rNEru8A')
      else
        attribute_data = 'data-'+value+'-enable'
        if player.attr(attribute_data) is "true"
          player.attr(attribute_data , "false")
        else
          player.attr(attribute_data , "true")

  unprepareVideo: ->
    sublime.unprepare @player[0]

  prepareAndPlayVideo: (autoplay) ->
    # Don't autoplay if it change the design
    if autoplay
      @player.attr('data-autoplay','true')
    else
      @player.removeAttr('data-autoplay')
    sublime.prepare @player[0]

  setupLightbox: (element) ->
    @lightboxPlayer = @player.clone().removeAttr('style class src')
      .attr('id','lightbox_home_demo')
      .attr('width','800').attr('height','450')
      .hide()
      .appendTo('section.demo-video-player')
    $(element).attr('href','#lightbox_home_demo')
    sublime.prepare element

