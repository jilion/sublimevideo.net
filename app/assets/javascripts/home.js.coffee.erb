SublimeVideo.homeReady = ->
  SublimeVideo.setupDemo()
  SublimeVideo.setupQuotesSlideshow()
  SublimeVideo.pickRandomVideoThumb()
  # (new SublimeVideo.Quotes).randomShow() if $('section.showcase').exists()

SublimeVideo.setupDemo = ->
  unless SublimeVideo.homeDemo?
    SublimeVideo.homeDemo = new SublimeVideo.HomeDemo('#home_demo_player')

  sublime.ready ->
    SublimeVideo.homeDemo.setupObservers()

SublimeVideo.setupQuotesSlideshow = ->
  if $('#customers-quotes-slideshow').exists()
    unless SublimeVideo.quotesSlideshow?
      SublimeVideo.quotesSlideshow = new SublimeVideo.QuotesSlideshow('#customers-quotes-slideshow', 3)
    SublimeVideo.quotesSlideshow.start()

class SublimeVideo.QuotesSlideshow
  constructor: (@slideshowDivSelector, @delay = 9) ->

  start: ->
    @stop()
    $("#{@slideshowDivSelector} > li:gt(0)").hide().addClass('blur')
    @interval = setInterval =>
      @nextQuote()
    , @delay*1000

  stop: ->
    clearInterval @interval

  nextQuote: ->
    $el = $("#{@slideshowDivSelector} > li:first")
    $el.addClass('blur').fadeOut 800, =>
      $el.next().fadeIn(800).removeClass('blur')
      $el.appendTo(@slideshowDivSelector)

# SublimeVideo.postersPaths = <%= (1..6).inject([]) { |memo, n| memo << image_path("home/video/posters/poster_#{n}.jpg") } %>

SublimeVideo.pickRandomVideoThumb = ->
  if ($homeVideo = $('#home_video')).exists()
    imgIndex = Math.floor(Math.random()*6)

    thumbs = $homeVideo.find('img.video_thumb')
    thumbs.hide()

    # Lightbox thumb
    $(thumbs[imgIndex]).show()
    $homeVideo.show()

    # Lightbox poster
    unless SublimeVideo.isMobile()
      posterImage = new Image()
      posterImage.src = SublimeVideo.postersPaths[imgIndex]
    $video = $("#video_horizon")
    $video.attr("poster", SublimeVideo.postersPaths[imgIndex])

SublimeVideo.fadeInHackForChrome = ($div) ->
  if /Chrome/i.test(navigator.userAgent)
    $div.css('left', '49px')
    setTimeout((-> $div.css('left', '50px')), 10)


class SublimeVideo.HomeDemo
  constructor: (playerId) ->
    @playerId = playerId

  reload: ->
    @player = $(@playerId)
    @featuresButtons = $('ul.demo-video-actions')
    @kitsButtons = $('ul.demo-choose-design')

  setupObservers: ->
    @reload()

    @kitsButtons.find('li a').each (index, element) =>
      @manageTogglerBtn($(element), 'kit')

    @featuresButtons.find('li a').each (index, element) =>
      $element = $(element)
      if $element.data('feature') == 'lightbox'
        @setupLightbox($element)
      else
        @manageTogglerBtn($element, 'feature')

  manageTogglerBtn: ($element, attribute) ->
    autoplay = true
    $element.on 'click', (event) =>
      event.preventDefault()
      if attribute is "kit"
        @kitsButtons.find('li').removeClass('active')
        autoplay = false
      @setupAttribute $element.data(attribute), autoplay
      $element.parent().toggleClass('active')

  setupAttribute: (value, autoplay) ->
    @unprepareVideo()
    @updatePlayersValues(@player, value)
    @updatePlayersValues(@lightboxPlayer, value)
    @prepareAndPlayVideo(autoplay)

  updatePlayersValues: (player, value) ->
    switch value
      when 9, 10, 11
        player.attr('data-player-kit',value)
      when "youtube"
        if player.attr('data-youtube-id')?
          player.removeAttr('data-youtube-id')
        else
          player.attr('data-youtube-id','rAq2rNEru8A')
      else
        attribute_data = 'data-'+value+'-enable'
        if player.attr(attribute_data) is "true"
          player.attr(attribute_data , "false")
        else
          player.attr(attribute_data , "true")

  unprepareVideo: ->
    sublime.unprepare @player[0]

  prepareAndPlayVideo: (autoplay) ->
    # Don't autoplay if it change the design
    if autoplay
      @player.attr('data-autoplay','true')
    else
      @player.removeAttr('data-autoplay')
    sublime.prepare @player[0]

  setupLightbox: ($element) ->
    $element.attr('href','#lightbox_home_demo')

    @lightboxPlayer = @player.clone().removeAttr('style class src')
      .attr('id','lightbox_home_demo')
      .attr('width','800').attr('height','450')
      .hide()
      .appendTo('section.demo-video-player')

    sublime.prepare $element.id, (lightbox) ->
      lightbox.on 'didClose', ->
        $element.parent().removeClass('active')

    $element.on 'click', (event) ->
      $element.parent().addClass('active')

